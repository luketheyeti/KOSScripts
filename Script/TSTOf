//2 stage single stick and a fairing launch script.
//semi advanced roll control
//no throttle control
//semi advanced gravity turn, vel/apo based
//uses circ
//needs minimum targetalt

parameter Direction.
parameter TargetAlt.

clearscreen.
lock throttle to 1.
lock steering to heading (0,90).


//countdown

run countfrom(5).

print "blastoff!!".
stage.


when ship:maxthrust < 1 then {
 print "staging!".
 wait 1.
 stage.
 preserve.
}

when ship:altitude >= 45000 then {
 print "losing fairings".
 stage.
 wait 1.
}

//Roll Program
wait until ship:velocity:surface:mag > 40.
print "starting roll program".
lock steering to heading((Direction / 2),90).
wait (Direction/180).
lock steering to heading(Direction,90).
wait 1.
lock steering to heading(Direction,90).

wait until ship:velocity:surface:mag >= 90.

//Ascent 1
declare Pitch to 89.
declare AscVel to 100.

until Pitch <= 45{ 
 until ship:velocity:surface:mag >= AscVel{
  clearscreen.
  print "Velocity: " + round(ship:velocity:surface:mag).
  print "target velocity: " + AscVel.
  print "altitude: " + round(ship:altitude).
  print "pitch: " + Pitch.
  wait .1.

 }

 //increment by  
 lock steering to heading(Direction,Pitch).
 set Pitch to (Pitch - 1).
 Set AscVel to (AscVel + 7).
 
}

//////////
//Ascent 2
set Pitch to (Pitch - 1).
declare AscApo to 20000.

until Pitch <= 10 or ship:apoapsis >= 60000{ 
 until ship:apoapsis >= AscApo{
  clearscreen.
  print "apo: " + round(ship:apoapsis).
  print "target apo: " + AscApo.
  print "altitude: " + round(ship:altitude).
  print "pitch: " + Pitch.
  wait .1.
 }

 lock steering to heading (Direction,Pitch).
 set Pitch to (Pitch - 1).
 set AscApo to (AscApo + 1000).
 //set AscApo to (AscApo + 700).

}

//apo out of atmo
until ship:apoapsis >= 70000{ 
 clearscreen.
 print "apo: " + round(ship:apoapsis).
 print "altitude: " + round(ship:altitude).
 print "target apo: " + TargetAlt.
 print "pitch: " + Pitch.

 wait .1.

}

////////////
//Final Burn
//wait until ship:apoapsis >= TargetAlt.
until ship:apoapsis >= TargetAlt{
 clearscreen.
 print "apo: " + round(ship:apoapsis).
 print "tApo: " + TargetAlt.
 wait .1.
}

/////
lock throttle to 0.
/////
lock steering to prograde.
clearscreen.
print "apo at destination, throttle off".
wait 1.
print "final apo: " + round(ship:apoapsis).
wait 1.
print "target alt: " + TargetAlt.
wait 5.


//Clear Atmo
until ship:altitude >= 71000{
 clearscreen.
 print "waiting to clear atmo".
 print "alt: " + round(ship:altitude).
 print "Talt: 71000".
 wait .1.
}

clearscreen.
print "welcome to space!".
wait 1.


//circularize
run Circularize(Direction).

//eccentricity
run eccentricity.

//ship stats

print round(stage:liquidfuel).

print "goodbye".


